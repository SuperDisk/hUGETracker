;; hUGETracker playback routine
;; Written by SuperDisk 2019

include "hardware.inc"
include "constants.inc"
include "debug_macros.inc"

add_a_to_r16: MACRO
    add a, \2
    ld \2, a
    adc a, \1
    sub \2
    ld \1, a
ENDM

;; TODO: See if there's a way to shave off a byte.
sub_from_r16: MACRO ;; (high, low, value)
    ld a, \2
    sub \3
    ld \2, a
    ld a, \1
    sbc a, 0
    ld \1, a
ENDM

add_a_to_hl: MACRO
    add_a_to_r16 h, l
ENDM

add_a_to_de: MACRO
    add_a_to_r16 d, e
ENDM

; Constants
STACK_SIZE EQU 20
;; Stack starts at $FFFE

PATTERN_LENGTH EQU 64
TICKS EQU 7

; $0000 - $003F: RST handlers.

SECTION "restarts", ROM0[$0000]
ret
REPT 7
    nop
ENDR
; $0008
ret
REPT 7
    nop
ENDR
; $0010
ret
REPT 7
    nop
ENDR
; $0018
ret
REPT 7
    nop
ENDR
; $0020
ret
REPT 7
    nop
ENDR
; $0028
ret
REPT 7
    nop
ENDR
; $0030
ret
REPT 7
    nop
ENDR
; $0038
ret
REPT 7
    nop
ENDR

; Interrupt addresses
SECTION "Vblank interrupt", ROM0[$0040]
    reti

SECTION "LCD controller status interrupt", ROM0[$0048]
    jp _dosound
    reti

SECTION "Timer overflow interrupt", ROM0[$0050]
    reti

SECTION "Serial transfer completion interrupt", ROM0[$0058]
    reti

SECTION "P10-P13 signal low edge interrupt", ROM0[$0060]
    reti

; Reserved stack space
SECTION "Stack", HRAM[$FFFE - STACK_SIZE]
    ds STACK_SIZE


; Control starts here, but there's more ROM header several bytes later, so the
; only thing we can really do is immediately jump to after the header
SECTION "init", ROM0[$0100]
    nop
    jp $0150

SECTION "romname", ROM0[$0134]
; $0134 - $013E: The title, in upper-case letters, followed by zeroes.
DB "HUGE"
DS 7 ; padding
; $013F - $0142: The manufacturer code. Empty for now
DS 4
DS 1
; $0144 - $0145: "New" Licensee Code, a two character name.
DB "NF"

SECTION "Song Data", ROM0
;; order_cnt is the number of orders times 2
order_cnt: db 2
order1: dw unreal ;; unreal ;; unreal;, lunawaves, lunawaves, unreal
order2: dw unreal_arps ;; empty, unreal, unreal, lunawaves
order3: dw empty
order4: dw empty

; lunawaves:
; include "lunawaves.inc"
unreal:
include "unreal.inc"
unreal_arps:
include "unreal_arps.inc"
tfau_arps:
include "tfau_arps.inc"
tfau_bass:
include "tfau_bass.inc"
empty:
rept 64
    db 90, 0, 0
endr
drums:
rept 16
    db 44, 0, 0
    db 90, 0, 0
    db 90, 0, 0
    db 90, 0, 0
endr
slideup:
  dn C4, 00, $000
rept 63
    dn 90, 00, $101
endr
arps:
rept 64
    dn C5, 00, $047
endr
vib:
    dn C5, 00, $000
rept 63
    dn 90, 00, $443
endr
updown:
    dn C4, 00, $105
rept 31
    dn 90, 00, $105
endr
rept 32
    dn 90, 00, $205
endr
volset:
    dn C5, 00, $000
rept 32
    dn 90, 00, $C90
    dn 90, 00, $CF0
endr
notecut:
rept 32
    dn C5, 00, $E04
    dn 90, 00, $000
endr

SECTION "Playback variables", WRAM0
pattern1: dw
pattern2: dw
pattern3: dw
pattern4: dw

current_order: dw

;; TODO: Find some way to get rid of this thing.
temp_note_value: dw

;; Size of a channel in bytes
CHANNEL_SIZE EQU 8
;; Amount to be shifted in order to skip a channel.
CHANNEL_SIZE_EXPONENT EQU 3

channels:

;;;;;;;;;;;
;;Channel 1
;;;;;;;;;;;
channel1:
channel_period1: dw
toneporta_target1: dw
channel_note1: db
vibrato_tremolo_phase1: db
envelope1: db
ds 1

;;;;;;;;;;;
;;Channel 2
;;;;;;;;;;;
channel2:
channel_period2: dw
toneporta_target2: dw
channel_note2: db
vibrato_tremolo_phase2: db
envelope2: db
ds 1

;;;;;;;;;;;
;;Channel 3
;;;;;;;;;;;
channel3:
channel_period3: dw
toneporta_target3: dw
channel_note3: db
vibrato_tremolo_phase3: db
envelope3: db
ds 1

;;;;;;;;;;;
;;Channel 4
;;;;;;;;;;;
channel4:
channel_period4: dw
toneporta_target4: dw
channel_note4: db
vibrato_tremolo_phase4: db
envelope4: db
ds 1

row: ds 1
tick: ds 1

; Initialization
SECTION "main", ROM0[$0150]
jp _init

_paint_tile:
    ld a, b
    ld [hl+], a
    ld a, c
    ld [hl+], a
    ret

_init:
    ; Set LCD palette for grayscale mode; yes, it has a palette
    ld a, %11100100
    ld [$FF00+$47], a

    ;; Fill with pattern
    ld hl, $8000
    ld bc, `10000000
    call _paint_tile
    ld bc, `01000000
    call _paint_tile
    ld bc, `00100000
    call _paint_tile
    ld bc, `00010000
    call _paint_tile
    ld bc, `00001000
    call _paint_tile
    ld bc, `00000100
    call _paint_tile
    ld bc, `00000010
    call _paint_tile
    ld bc, `00000001
    call _paint_tile

    ;; Load some wave data (or code) into _AUD3WAVERAM
    ld hl, $150 ;; note_table
_addr = _AUD3WAVERAM
    REPT 16
    ld a, [hl+]
    ldh [_addr], a
_addr = _addr + 1
    ENDR

    ;; Enable chanel 3
    ; ld a, $80
    ; ldh [rAUD3ENA], a
    ; ld a, $ff
    ; ldh [rAUD3LEN], a
    ; ld a, $20
    ; ldh [rAUD3LEVEL], a

    ld a, %11110000
    ld [envelope1], a
    ld a, %11110000
    ld [envelope2], a

    ; Enable sound globally
    ld a, $80
    ldh [rAUDENA], a
    ; Enable channel 1 in stereo
    ld a, $FF
    ldh [rAUDTERM], a
    ; Set volume
    ld a, $77
    ldh [rAUDVOL], a

    ld c, 0 ;; Current order index
    call _refresh_patterns

    ;; Enable the timer
    ld a, TACF_START | TACF_4KHZ
    ld [rTAC], a

    ;; Enable the HBlank interrupt on scanline 0
    ld a, [rSTAT]
    or a, STATF_LYC
    ld [rSTAT], a
    xor a ; ld a, 0
    ld [rLYC], a

    ld a, IEF_LCDC ; IEF_VBLANK;  | IEF_LCDC ; IEF_HILO | IEF_TIMER
    ld [rIE], a
    ei

    jp _halt

_refresh_patterns:
;; Loads pattern registers with pointers to correct pattern based on
;; an order index

;; Call with c set to what order to load
load_pattern: MACRO
    ld hl, 0
    ld l, c
    ld de, \1
    add hl, de

    ld a, [hl+]
    ld [\2], a
    ld a, [hl+]
    ld [\2+1], a
ENDM
    load_pattern order1, pattern1
    load_pattern order2, pattern2
    load_pattern order3, pattern3
    load_pattern order4, pattern4
    ret

_lookup_note:
;; Call with:
;; Pattern pointer in BC
;; channel_noteX pointer in DE

;; Stores note period value in HL
;; Stores instrument/effect code in B
;; Stores effect params in C
;; Stores note number in the memory pointed to by DE

    ld a, [row]
    ld h, a
    ;; Multiply by 3 for the note value
    add h
    add h

    add 2
    ld h, 0
    ld l, a
    add hl, bc ; HL now points at the 3rd byte of the note
    ld a, [hl-]
    ld c, a
    ld a, [hl-]
    ld b, a

    ld a, [hl]

    ;; Store the loaded note value in channel_noteX
    ld h, d
    ld l, e
    ld [hl], a

    ;; If the note we found is greater than LAST_NOTE, then it's not a valid note
    ;; and nothing needs to be updated.
    cp LAST_NOTE
    ret nc

_convert_note:
    ;; Call with:
    ;; Note number in A
    ;; Stores note period value in HL

    add a ;; double it to get index into hi/lo table

    ld hl, note_table
    add_a_to_hl
    ld     a, [hl+]
    ld     h, [hl]
    ld     l, a

    ;; Need to clear the carry flag-- this complements it, but since the last
    ;; thing that modified the flag always sets it, this will clear it.
    ccf
    ret

_update_channel:
;; Call with:
;; Channel in B
;; Note tone in DE

;; TODO: Turn this into a jump table? Or find some other way to optimize
    ld a, b

    cp 0
    jp z, _update_channel1
    cp 1
    jp z, _update_channel2
    cp 2
    jp z, _update_channel3
    cp 3
    jp z, _update_channel4

_update_channel1:
    ld a, e
    ldh [rAUD1LOW], a
    ld a, d
    ldh [rAUD1HIGH], a
    ret
_update_channel2:
    ld a, e
    ldh [rAUD2LOW], a
    ld a, d
    ldh [rAUD2HIGH], a
    ret
_update_channel3:

    ret
_update_channel4:

    ret

strip_instrument: MACRO
    ld a, b
    and %00001111
ENDM

;;;;;;;
;; Note playing routines. Need to either consolidate these or find some
;; better way of doing effects that happen on the first tick.
;;;;;;;

_playnote1:
;; Call with:
;; Note value in H and L

;; Free: A, D, E, B, C

;; Play a note on channel 1 (square wave)
    ; ld a, %11110000
    ; ld [rAUD1ENV], a

    ld a, %01111111
    ld [rAUD1LEN], a

    ld a, l ; [temp_note_value+1]
    ld [channel_period1], a
    ld [rAUD1LOW], a

    ld a, h ; [temp_note_value]
    ld [channel_period1+1], a

    or %10000000
    ld [rAUD1HIGH], a

    ret

_playnote2:
;; Call with:
;; Note value in H and L
;; instrument + effect code in B

;; Play a note on channel 2 (square wave)
    ; ld a, %11110000
    ; ld [rAUD2ENV], a

    ld a, %01111111 ; $10
    ld [rAUD2LEN], a

    ld a, l ; [temp_note_value+1]
    ld [channel_period2], a
    ld [rAUD2LOW], a

    ld a, h ; [temp_note_value]
    ld [channel_period2+1], a

    or %10000000
    ld [rAUD2HIGH], a

    ret

_playnote3:
;; Call with:
;; Note value in H and L
;; instrument + effect code in B

;; Play a note on channel 3 (waveform)
    ld a, l ; [temp_note_value+1]
    ld [channel_period3], a
    ld [rAUD3LOW], a

    ld a, h ; [temp_note_value]
    ld [channel_period3+1], a

    or %10000000
    ld [rAUD3HIGH], a

    ret

_playnote4:
;; Call with:
;; Note value in H and L
;; instrument + effect code in B

;; Play a "note" on channel 4 (noise)

    ld a, %00011111
    ld [rAUD4LEN], a
    ld a, %11110001
    ld [rAUD4ENV], a
    ld a, l; %01000100
    ld [rAUD4POLY], a
    ld a, %10000000
    ld [rAUD4GO], a

    ret

_doeffect:
    ;; Call with:
    ;; B: instrument nibble + effect type nibble
    ;; C: effect parameters
    ;; E: channel

    ;; free: A, D, H, L

    ;; Strip the instrument bits off leaving only effect code
    ld a, b
    and %00001111
    ld b, a

    ;; Multiply by 3 to get offset into table
    ld a, b
    add a, b
    add a, b

    ld hl, .jump

    add a, l
    ld l, a
    adc a, h
    sub l
    ld h, a

    ld b, e
    ld a, [tick]
    cp 0
    ; or a ; We can return right off the bat if it's tick zero
    jp hl

.jump:
    ;; Jump table for effect
    jp fx_arpeggio                     ;0xy
    jp fx_porta_up                     ;1xy
    jp fx_porta_down                   ;2xy
    jp fx_toneporta                    ;3xy
    jp fx_vibrato                      ;4xy
    jp fx_no_op ;fx_toneporta_volslide ;5xy
    jp fx_no_op ;fx_vibrato_volslide   ;6xy
    jp fx_no_op ;fx_tremolo            ;7xy
    jp fx_no_op ;fx_set_pan            ;8xy
    jp fx_no_op ;fx_set_duty           ;9xy
    jp fx_no_op ;fx_vol_slide          ;Axy
    jp fx_no_op ;fx_pos_jump           ;Bxy
    jp fx_set_volume                   ;Cxy
    jp fx_no_op ;fx_pattern_break      ;Dxy
    jp fx_note_cut                     ;Exy
    jp fx_no_op ;fx_set_speed          ;Fxy

setup_channel_pointer:
    ;; Call with:
    ;; Channel value in B
    ;; Offset in D
    ;; Returns value in HL

    ld a, b
    REPT CHANNEL_SIZE_EXPONENT
        sla a
    ENDR
    add d
    ld hl, channels
    add_a_to_hl
    ret

return_if_tick_zero: MACRO
    ret z
ENDM

fx_no_op:
    ret

;; TODO: Fix this
fx_note_cut:
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L
    ;; ld a, [tick]
    cp c
    ret nz

    ld d, 0
    jp set_channel_volume

fx_set_volume:
    ret nz ;; Return if we're not on tick zero.

    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ;; Arguments to this effect will be massaged to correct form for the channel
    ;; in the editor so we don't have to AND and SWAP and stuff.

set_channel_volume:
    ;; Call with:
    ;; Correct volume value in D
    ;; Channel number in B

    ld a, b
    cp 1 ; check if it's channel 2
    jr c, set_chn_1_vol
    jr z, set_chn_2_vol
set_chn_3_vol:
    ld a, d
    ld [rAUD3LEVEL], a

    ret
set_chn_2_vol:
    ; ld a, d
    ; and %00001111
    ; or c
    ld a, c
    ld [rAUD2ENV], a

    ret
set_chn_1_vol:
    ld a, d
    and %00001111
    or c
    ld [rAUD1ENV], a

    ret

fx_vibrato:
    return_if_tick_zero
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ;; Extremely poor man's vibrato.
    ;; Speed values:
    ;; (0x0  = 1.0)
    ;; (0x1  = 0.5)
    ;; (0x3  = 0.25)
    ;; (0x7  = 0.125)
    ;; (0xf  = 0.0625)
    ld d, 4
    call setup_channel_pointer

    ld a, c
    and %11110000
    swap a
    ld d, a

    ld a, [tick] ; Probably should change to some constantly ticking thing
    and d
    ld a, [hl-]
    jr z, .go_up
.restore:
    dec hl
    dec hl
    ld a, [hl-]
    ld l, [hl]
    ld h, a
    jr .finish_vibrato
.go_up:
    call _convert_note ; TODO: Find a way to grab it directly rather than recompute
    ld a, c
    and %00001111
    add_a_to_hl
.finish_vibrato:
    ld d, h
    ld e, l
    jp _update_channel

fx_arpeggio:
    return_if_tick_zero
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ld d, 4
    call setup_channel_pointer

    ld a, [tick]
    dec a

    ;; Multiply by 2 to get offset into table
    add a

    ld d, [hl]

    ld hl, .arp_options
    add_a_to_hl
    jp hl

    ;; TODO: Make this work when TICKS != 6
.arp_options:
    jr .set_arp1
    jr .set_arp2
    jr .reset_arp
    jr .set_arp1
    jr .set_arp2
.reset_arp:
    ld a, d
    jr .finish_skip_add
.set_arp2:
    ld a, c
    swap a
    jr .finish_arp
.set_arp1:
    ld a, c
.finish_arp:
    and %00001111
    add d
.finish_skip_add:
    call _convert_note
    ld d, h
    ld e, l
    jp _update_channel

fx_porta_up:
    return_if_tick_zero
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ld d, 0
    call setup_channel_pointer

    ld a, [hl+]
    ld e, a
    ld d, [hl]

    ld a, c
    add_a_to_de

    ld a, d
    ld [hl-], a
    ld [hl], e

    jp _update_channel

;; TODO: Maybe merge with fx_porta_up, since they're so similar? Would need
;; to find a way to compare against effect code.
fx_porta_down:
    return_if_tick_zero
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ld d, 0
    call setup_channel_pointer

    ld a, [hl+]
    ld e, a
    ld d, [hl]

    ld a, c
    sub_from_r16 d, e, c

    ld a, d
    ld [hl-], a
    ld [hl], e

    jp _update_channel

;; TODO: Figure out why this is not toneporta-ing as much as it used to. so bizarre...
fx_toneporta:
    jr nz, .do_toneporta

    ;; We're on tick zero, so just move the temp note value into the toneporta target.
    ld d, 4
    call setup_channel_pointer

    ;; If the note is nonexistent, then just return
    ld a, [hl-]
    cp LAST_NOTE
    jr nc, .return_skip

    ld a, [temp_note_value+1]
    ld c, a
    ld a, [temp_note_value]

    ld [hl-], a
    ld [hl], c

    ;; Don't call _playnote. This is done by grabbing the return
    ;; address and manually skipping the next call instruction.
.return_skip:
    pop hl
    pop af
    ld a, 14
    add_a_to_hl
    jp hl

.do_toneporta:
    ;; B: channel
    ;; C: effect parameters

    ;; free registers: A, D, E, H, L

    ;; TODO: Optimize. This usage of the stack isn't great but IMO it's
    ;; better than using the stack hack which forces all effects to screw with
    ;; pushing and popping. Most effects won't need to do this.
    ld d, 0
    call setup_channel_pointer
    push hl

    ld a, [hl+]
    ld e, a
    ld a, [hl+]
    ld d, a

    ld a, [hl+]
    ld h, [hl]
    ld l, a

    ;; Comparing which direction to move the current value
    ;; TODO: Optimize this!!!!

    ;; Compare high byte
    ld a, h

    cp d
    jr c, .subtract ; target is less than the current period
    jr z, .high_byte_same
    jr .add
.high_byte_same:
    ld a, l
    cp e
    jr c, .subtract ; the target is less than the current period
    jr z, .done ; both nibbles are the same so no portamento
.add:
    ld a, c
    add a, e
    ld e, a
    adc a, d
    sub e
    ld d, a

    ld a, h
    cp d
    jr c, .set_exact
    ld a, l
    cp e
    jr c, .set_exact

    jr .done

.subtract:
    ld a, e
    sub c
    ld e, a

    ld a, d
    sbc a, 0
    ld d, a

    ld a, h
    cp d
    jr nc, .set_exact
    ld a, l
    cp e
    jr nc, .set_exact

    jr .done

.set_exact:
    ld d, h
    ld e, l
.done:
    pop hl
    ld a, e
    ld [hl+], a
    ld [hl], d

    jp _update_channel

loadShort: MACRO
    ld a, [\1]
    ld \3, a
    ld a, [\1 + 1]
    ld \2, a
ENDM

_dosound:
    ld a, [tick]
    or a
    jp nz, .process_effects

    ;; remove this
    ld a, %11110000
    ld [rAUD1ENV], a

    loadShort pattern1, b, c
    call _lookup_note

    ld a, h
    ld [temp_note_value], a
    ld a, l
    ld [temp_note_value+1], a

    push af

    ld e, 0
    call _doeffect

    pop af

    jr nc, .after_note1

    ld a, [temp_note_value]
    ld h, a
    ld a, [temp_note_value+1]
    ld l, a

    call _playnote1 ; 3 bytes

.after_note1:
    ;; remove this
    ld a, %11110000
    ld [rAUD2ENV], a

    loadShort pattern2, b, c
    call _lookup_note

    ld a, h
    ld [temp_note_value], a
    ld a, l
    ld [temp_note_value+1], a

    push af

    ld e, 1
    call _doeffect

    pop af

    jr nc, .after_note2

    ld a, [temp_note_value]
    ld h, a
    ld a, [temp_note_value+1]
    ld l, a

    call _playnote2 ; 3 bytes

    ; loadShort pattern2, b, c
    ; call _lookup_note
    ; jr nc, .after_note2

    ; ld a, h
    ; ld [temp_note_value], a
    ; ld a, l
    ; ld [temp_note_value+1], a

    ; ld e, 1
    ; call _doeffect

    ; call _playnote2

.after_note2:
    ; loadShort pattern3, b, c
    ; call _lookup_note
    ; jr nc, .after_note3

    ; ld a, h
    ; ld [temp_note_value], a
    ; ld a, l
    ; ld [temp_note_value+1], a

    ; ld e, 2
    ; call _doeffect

    ; call _playnote3

.after_note3:
    ; loadShort pattern4, b, c
    ; call _lookup_note
    ; jr nc, process_tick

    ; ld a, h
    ; ld [temp_note_value], a
    ; ld a, l
    ; ld [temp_note_value+1], a

    ; ld e, 3
    ; call _doeffect

    ; call _playnote4

    jp process_tick

.process_effects:
    ;; Only do effects if not on tick zero

    loadShort pattern1, b, c
    call _lookup_note

    ld a, c
    cp 0
    jr z, .after_effect1

    ld e, 0
    call _doeffect

.after_effect1:
    loadShort pattern2, b, c
    call _lookup_note

    ld a, c
    cp 0
    jr z, .after_effect2

    ld e, 1
    call _doeffect

.after_effect2:
    ; loadShort pattern3, b, c
    ; call _lookup_note
    ; ld e, 2
    ; call _doeffect

.after_effect3:
    ; loadShort pattern4, b, c
    ; call _lookup_note
    ; ld e, 3
    ; call _doeffect

.after_effect4:

process_tick:
    ld a, [tick]
    inc a

    cp TICKS
    jp z, _newrow

    ld [tick], a
    reti

_newrow:
    ;; Reset tick to 0
    xor a ; ld a, 0
    ld [tick], a

    ;; Increment row.
    ld a, [row]
    inc a
    cp PATTERN_LENGTH
    jr nz, .noreset

    ;; Increment order and change loaded patterns
    ld a, [order_cnt]
    ld b, a
    ld a, [current_order]
    add a, 2
    cp b
    jr nz, .update_current_order
    xor a ; ld a, 0
.update_current_order:
    ld [current_order], a
    ld c, a

    call _refresh_patterns

    xor a ; ld a, 0
    ld [row], a
    reti

.noreset:
    ld [row], a

    reti

_halt:
    ; Do nothing, forever
    halt
    nop
    jr _halt

SECTION "Wave stuff", ROM0

SECTION "Note Table", ROM0
note_table:
include "music.inc"